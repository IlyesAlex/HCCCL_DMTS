#Saving logfile
string logpath=logfile_directory;
string fn=logpath+"DMTS_"+logfile.subject()+".txt";
bool fe0=file_exists(fn);
output_file ofile1=new output_file;
ofile1.open_append(fn);
if(!fe0) then
    ofile1.print("Trial\t");
    ofile1.print("Response\t");
    ofile1.print("Correct\t");
    ofile1.print("Correct_resp\n");
end;

#Read in text file
input_file in = new input_file;
in.open("stimuliB.txt");

array<string>stimlist[0][0];
loop until in.end_of_file() begin
	 string this_line = in.get_line();
	 array<string> temp[0];
	 this_line.split("\t", temp );
	 stimlist.add( temp );
end;

#Trials
instructions_trial.present();

loop int i = 1 until i > stimlist.count() - 2 begin
	 string target = stimlist[i+1][1];
	 string var1 = stimlist[i+1][2];
	 string var2 = stimlist[i+1][3];
	 string var3 = stimlist[i+1][4];
	 string var4 = stimlist[i+1][5];
	 string correct = stimlist[i+1][6];
	 int correct_int = int(correct);
	 #Blank
	 blank_trial.present();
	 #Target
    target_bitmap.set_filename(target);
	 target_bitmap.load();
	 target_trial.present();
	 #Mask
    mask_trial.present();
	 #Choices
	 first_choice.set_filename(var1);
	 first_choice.load();
	 second_choice.set_filename(var2);
	 second_choice.load();
	 third_choice.set_filename(var3);
	 third_choice.load();
    fourth_choice.set_filename(var4);
    fourth_choice.load();
    choice_trial.present();
    #Response collecting
    stimulus_data last=stimulus_manager.last_stimulus_data();
    int resp_button=last.button();
    int correct_resp;
    if resp_button == correct_int then
	     correct_resp = 1;
    else
	     correct_resp = 0;
	 end;
    #Logging
    ofile1.print(string(i) + "\t");
    ofile1.print(string(resp_button) + "\t");
    ofile1.print(correct + "\t");
    ofile1.print(string(correct_resp) + "\n");
    
    i = i+1;

end;